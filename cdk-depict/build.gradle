/*
 * Copyright (c) 2023 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'java'
    id 'application'
    id 'com.bmuschko.docker-java-application' version '7.3.0'
}

import static com.bmuschko.gradle.docker.tasks.image.Dockerfile.*

description = 'Molecule depiction using CDK'

dependencies {

    implementation 'commons-cli:commons-cli:1.5.0'

    def cdkVersion = '2.9'

    implementation "org.openscience.cdk:cdk-core:$cdkVersion"
    implementation "org.openscience.cdk:cdk-data:$cdkVersion"
    implementation "org.openscience.cdk:cdk-silent:$cdkVersion"
    implementation "org.openscience.cdk:cdk-io:$cdkVersion"
    implementation "org.openscience.cdk:cdk-ioformats:$cdkVersion"
    implementation "org.openscience.cdk:cdk-smiles:$cdkVersion"
    implementation "org.openscience.cdk:cdk-renderbasic:$cdkVersion"
    implementation "org.openscience.cdk:cdk-renderawt:$cdkVersion"
    implementation "org.openscience.cdk:cdk-depict:$cdkVersion"
    implementation "org.openscience.cdk:cdk-legacy:$cdkVersion"

    testImplementation "org.codehaus.groovy:groovy-all:2.5.7"
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}

application {
    // Define the main class for the application.
    // this can be set as a commandline option e.g.
    // ./gradlew run --args="--help" -PmainClass=org.foo.Bar
    mainClass = 'org.squonk.cdk.depict.Mol2Image'
}


docker {
    javaApplication {
        baseImage = 'openjdk:11-jre'
        maintainer = 'Tim Dudgeon "tdudgeon@informaticsmatters.com"'
        ports = []
        images = ["squonk/cdk-depict:${dockerImageTag}"]
        mainClassName = 'squonk.cdk.CDKMolDepict'
    }
}

// This modifies the Dockerfile that is build by the docker-java-application plugin.
// The ENTRYPOINT is replaced with 'java' and the classpath arguments
// A CMD added that is '-version'.
// The expectation is that the CMD will be overridden at runtime.
dockerCreateDockerfile {
    List<Instruction> originalInstructions = new ArrayList<Instruction>(instructions.get())
    int entrypointIndex = originalInstructions
            .findIndexOf { it.keyword == EntryPointInstruction.KEYWORD}
    originalInstructions.remove(entrypointIndex)
    originalInstructions.add(new DefaultCommandInstruction("java", "-version"))
    originalInstructions.add(new EnvironmentVariableInstruction("CLASSPATH", "/app/resources:/app/classes:/app/libs/*"))

    instructions.set(originalInstructions)
}
