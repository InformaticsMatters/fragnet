/*
 * Copyright (c) 2019 Informatics Matters Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import com.bmuschko.gradle.docker.tasks.container.DockerCreateContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStartContainer
import com.bmuschko.gradle.docker.tasks.container.DockerStopContainer

plugins {
    id 'com.bmuschko.docker-remote-api'
    id 'com.bmuschko.docker-java-application'
}

description = 'Fragment network search'

String RDBASE = System.getenv('RDBASE')
String RDKitJarFile = RDBASE + '/Code/JavaWrappers/gmwrapper/org.RDKit.jar'
boolean RDKitJava = RDBASE && file(RDKitJarFile).exists()
println "RDKitJarFile exists? $RDKitJava"

apply plugin: 'war'
apply plugin: 'groovy'
apply plugin: 'application'
apply plugin: 'com.bmuschko.tomcat'


dependencies {

    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'

    compile "org.jboss.weld.servlet:weld-servlet-core:$weldVersion"
    
    compile "org.apache.camel:camel-core:$camelVersion"
    compile "org.apache.camel:camel-jackson:$camelVersion"
    compile "org.apache.camel:camel-servlet:$camelVersion"
    compile "org.apache.camel:camel-servletlistener:$camelVersion"
    compile "org.apache.camel:camel-cdi:$camelVersion"
    compile "org.apache.camel:camel-swagger:$camelVersion"
    compile "org.apache.camel:camel-swagger-java:$camelVersion"

    implementation "io.prometheus:simpleclient_common:0.6.0"

    if (RDKitJava) {
        println "RDBASE defined as $RDBASE - using that location for RDKit"
        println "Using jar file " + RDKitJarFile
        compile files(RDKitJarFile)
    } else {
        println "Using local org.RDKit.jar"
        compile files("../lib/rdkit/org.RDKit.jar")
    }

    compile 'org.neo4j.driver:neo4j-java-driver:1.7.2'
    
    providedCompile 'javax.servlet:javax.servlet-api:3.0.1'

    def tomcatVersion = '8.5.37'
    tomcat "org.apache.tomcat.embed:tomcat-embed-core:${tomcatVersion}",
            "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"


    testCompile "org.codehaus.groovy:groovy-all:2.5.7"
    testCompile "org.spockframework:spock-core:1.3-groovy-2.5"

}

tomcat {
    enableSSL = false
    httpProtocol = 'org.apache.coyote.http11.Http11Nio2Protocol'
    ajpProtocol  = 'org.apache.coyote.ajp.AjpNio2Protocol'
}

task createNeoContainer(type: DockerCreateContainer) {
    targetImageId 'informaticsmatters/fragnet-test:3.5'
    portBindings = ['7474:7474','7687:7687']
    envVars = ['NEO4J_dbms_memory_pagecache_size': '2g',
               'NEO4J_dbms_memory_heap_initial__size': '2g',
               'NEO4J_dbms_memory_heap_max__size': '2g',
               'NEO4J_AUTH': 'neo4j/test123',
               'NEO4J_USERNAME': 'neo4j',
               'NEO4J_PASSWORD': 'test123']
    autoRemove = true
}

task startNeoContainer(type: DockerStartContainer) {
    dependsOn createNeoContainer
    targetContainerId createNeoContainer.getContainerId()
}

task stopNeoContainer(type: DockerStopContainer) {
    targetContainerId createNeoContainer.getContainerId()
}

test {
    environment = [
            'NEO4J_PASSWORD':'test123',
            'NEO4J_SERVER':'localhost',
            'LD_LIBRARY_PATH':'/home/timbo/github/rdkit/rdkit/Code/JavaWrappers/gmwrapper:/usr/x86_64-linux-gnu',
            'RDBASE':'/home/timbo/github/rdkit/rdkit'
    ]
//    beforeTest { descriptor ->
//        logger.lifecycle("Running test: " + descriptor)
//    }
}

tasks.test.dependsOn("startNeoContainer")
tasks.test.finalizedBy("stopNeoContainer")

task runApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'org.squonk.fragnet.search.FragmentUtils'

    environment = [
            'NEO4J_PASSWORD':'test123',
            'NEO4J_SERVER':'localhost',
            'RDBASE':'/home/timbo/github/rdkit/rdkit',
            'LD_LIBRARY_PATH':'/home/timbo/github/rdkit/rdkit/Code/JavaWrappers/gmwrapper:/usr/x86_64-linux-gnu',

    ]

}

mainClassName = 'org.squonk.fragnet.search.queries.examples.AllMols'

dockerCreateDockerfile {
//    entryPoint('java', '-cp', '/app/resources:/app/classes:/app/libs/*:/rdkit/gmwrapper/org.RDKit.jar',
//            'org.squonk.fragnet.search.queries.examples.ExpandHits')
}

docker {
    javaApplication {
        baseImage = 'informaticsmatters/rdkit-java-debian:Release_2019_03'
        maintainer = 'Tim Dudgeon'
        tag = "tdudgeon/fragnetexpand:latest"
    }
}